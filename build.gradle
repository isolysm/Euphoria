buildscript {
    def mcVersionStr = project.name.tokenize('-')[0]
    def (major, minor, patch) = mcVersionStr.tokenize('.')
    def mcVersion = "${major}${minor.padLeft(2, '0')}${(patch ?: '').padLeft(2, '0')}" as int
    def fabric = mcVersion >= 11400 && project.name.endsWith("-fabric")
    project.ext.mcVersion = mcVersion
    project.ext.mcVersionStr = mcVersionStr
    project.ext.mcPlatform = fabric ? 'fabric' : 'forge'
    project.ext.fabric = fabric

    repositories {
        mavenLocal()
        mavenCentral()
        maven{url "https://maven.fabricmc.net/"}
        if (!fabric) maven {url "https://maven.minecraftforge.net/"}
        maven{url "https://oss.sonatype.org/content/repositories/snapshots/"}
        maven {url "https://jitpack.io"}
    }

    dependencies {
        if (fabric) {
            classpath 'fabric-loom:fabric-loom.gradle.plugin:0.11-SNAPSHOT'
        } else if (mcVersion >= 11400) {
            classpath('net.minecraftforge.gradle:ForgeGradle:5.1.26') { // exact version cause FG tends to break stuff
                exclude group: 'trove', module: 'trove' // preprocessor/idea requires more recent one
            }
        } else {
            def forgeGradleVersion = mcVersion >= 11200 ? '717a9f4' : // FG 2.3
                                            mcVersion >= 10904 ? '83163c1' : // FG 2.2
                                            'ceb83c0'   // FG 2.1

            classpath("com.github.ReplayMod:ForgeGradle:$forgeGradleVersion:all") {
                exclude group: 'net.sf.trove4j', module: 'trove4j' // preprocessor/idea requires more recent one
                exclude group: 'trove', module: 'trove' // different name same thing
            }
        }
    }
}